#!/usr/bin/env perl
# ABSTRACT: An API from the legacy Metabase::Web to the local database

=head1 SYNOPSIS

=head1 DESCRIPTION

=head1 SEE ALSO

=cut

package CPANTesters::Web::Legacy::Metabase;
use Mojo::Base '-base';
use Mojo::File 'path';
use Mojolicious::Lite;
use Data::FlexSerializer;
use Metabase::Fact;
use Mojo::JSON qw( to_json from_json );
use FindBin ();

=head1 CONFIG

The configuration file is set by the C<MOJO_CONFIG> environment
variable, or found in the C<etc/> directory and named C<metabase.conf>
or C<metabase.$mode.conf>. The configuration is a hashref with the
following keys:

=head2 db

The C<db> hashref contains the C<dsn>, C<user>, and C<pass> to connect
to the database.

To create a database for local testing (C<-m local>), run
C<< sqlite3 local.db < eg/legacy/schema.sql >>.

=cut

app->home( path( $FindBin::Bin )->dirname ) unless $ENV{MOJO_HOME};
app->moniker( 'metabase' );
my $root_conf = app->home->child( sprintf 'etc/%s.conf', app->moniker );
plugin Config => (
    file => $ENV{MOJO_CONFIG} || $root_conf,
);

=attr dbh

The database handle to write reports to.

=cut

helper dbh => sub {
    my ( $c, $new_dbh ) = @_;
    state $dbh = DBI->connect( @{ $c->app->config->{db} }{qw( dsn user pass )} );
    $dbh = $new_dbh if $new_dbh;
    return $dbh;
};

=route /beta/submit/CPAN-Testers-Metabase

=route /api/v1/submit/CPAN-Testers-Metabase

This route submits a new report into the local copy of the Metabase.
This is a shim that will remain in-place until all the CPAN Testers clients
are updated to submit reports via the new API (so, forever).

=cut

# This code was derived from CPAN::Testers::Data::Generator sub cache_report
# Once this is working, we can force CPAN::Testers::Data::Generator to
# ignore the Amazon SimpleDB Metabase by making the localonly flag
# always set to true.

# We are also mimicking parts of Metabase::Web and Metabase::Gateway

sub handle_post {
    my ( $c ) = @_;
    my $body = $c->req->json;

    ### Transform it for the current local Metabase
    # Thaw the Metabase report
    my $report = Metabase::Fact->class_from_type( $body->{metadata}{core}{type} )
        ->from_struct( $body );

    my $guid = $report->core_metadata->{guid};
    my $updated = $report->core_metadata->{update_time};

    # Encode it to JSON
    my %facts;
    for my $fact ( $report->facts ) {
        my $name = ref $fact;
        $facts{ $name } = $fact->as_struct;
        $facts{ $name }{ content } = from_json( $facts{ $name }{ content } );
    }

    # Serialize it to compress it using Data::FlexSerializer
    # "report" gets serialized with JSON
    my $json_zipper = Data::FlexSerializer->new(
        detect_compression  => 1,
        detect_json         => 1,
        output_format       => 'json'
    );
    my $report_zip = $json_zipper->serialize( \%facts );

    # "fact" gets serialized with Sereal
    my $sereal_zipper = Data::FlexSerializer->new(
        detect_compression  => 1,
        detect_sereal       => 1,
        output_format       => 'sereal'
    );
    my $fact_zip = $sereal_zipper->serialize( $report );

    $c->app->dbh->do(
        'INSERT INTO metabase (guid,updated,report,fact) VALUES (?,?,?,?)',
        {},
        $guid, $updated, $report_zip, $fact_zip,
    );

    $c->res->headers->location( '/guid/' . $guid );
    return $c->render(
        status => 201,
        json => { guid => $guid },
    );
};

post '/api/v1/submit/CPAN-Testers-Metabase' => \&handle_post;
post '/beta/submit/CPAN-Testers-Metabase' => \&handle_post;

app->start;
